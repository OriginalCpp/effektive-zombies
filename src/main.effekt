module main

import src/ffi
import src/map

val KEY_DOWN = "keydown"
val KEY_UP = "keyup"
val CANVAS_WIDTH = 1920
val CANVAS_HEIGHT = 1080

type Color {
  Green()
  Red()
}

def toString(color: Color): String = color match {
  case Green() => "green"
  case Red()   => "red"
}

type Drawable {
  Rect(pos: Vec2d, width: Int, height: Int, color: Color)
  Circle(pos: Vec2d, radius: Int, color: Color)
}

type GameObject {
  Player(hitbox: Drawable)
}

def getHitbox(object: GameObject): Drawable = object match {
  case Player(hitbox) => hitbox
}

interface Canvas {
  def get(): Node
  def draw(object: GameObject): Unit
  def clear(): Unit
}

def canvas[T] { function: () => T / {Canvas} }: T = {
  try {
    function()
  } with Canvas {
    def get = resume(getElementById("canvas").getOrElse { createCanvas(CANVAS_WIDTH, CANVAS_HEIGHT) })
    def draw(object) = {
      canvas { 
        object.getHitbox() match {
          case Rect(pos, width, height, color) => 
            drawRect(do get(), pos.x, pos.y, width, height, color.toString())
          case _ => ()
        } 
      }
      resume(())
    }
    def clear = {
      canvas { clear(do get()) }
      resume(())
    }
  }
}

def move(drawable: Drawable, dVec: Vec2d): Drawable = drawable match {
  case Rect(pos, w, h, c) => Rect(pos + dVec, w, h, c)
  case Circle(pos, r, c) => Circle(pos + dVec, r, c)
}

type Event = String

def main(): Unit = {
  val PLAYER_WIDTH = 100
  val PLAYER_HEIGHT = 100
  val PLAYER_SPEED = 0.4
  val keysPressed = emptyMap[String, Bool]()
  val player = ref[GameObject](
    Player(Rect(Vec2d(150.0, 100.0), PLAYER_WIDTH, PLAYER_HEIGHT, Green()))
  )
  val now = ref[Int](getTime())

  def getAxisMovement(minusKey: String, plusKey: String): Double = {
    val minus: Double = keysPressed.map::get(minusKey).getOrElse{false}.toDouble()
    val plus: Double = keysPressed.map::get(plusKey).getOrElse{false}.toDouble()
    -1.0 * minus + plus
  }

  def assemblePlayerMovement(): Vec2d = {
    val x = getAxisMovement("a", "d")
    val y = getAxisMovement("w", "s")
    normalize(Vec2d(x, y)) * PLAYER_SPEED
  }

  def move(object: Ref[GameObject], dt: Int): Unit = object.get match {
    case Player(hitbox) => object.set(Player(hitbox.move(dt * assemblePlayerMovement())))
  }

  def calcDt(): Int = {
    val timeElapsed = getTime() - now.get()
    now.set(getTime())
    timeElapsed
  }

  def render(deadline: IdleDeadline): Unit = {
    val dt = calcDt()
    player.move(dt)
    with canvas;
    do clear()
    do draw(player.get)
    requestIdleCallback(render)
  }

  def pressedHandler(event: Event): Unit = keysPressed.update(event, true)
  def releasedHandler(event: Event): Unit = keysPressed.update(event, false)

  addEventListener(KEY_DOWN, pressedHandler)
  addEventListener(KEY_UP, releasedHandler)
  requestIdleCallback(render)
}